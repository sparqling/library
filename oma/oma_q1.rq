######
### Q1: Get the OMA protein splice variants and their UniProt cross-references of orthologs of ENSG00000244734 gene  
######
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX ensembl: <http://rdf.ebi.ac.uk/resource/ensembl/>
PREFIX orth: <http://purl.org/net/orth#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX taxon: <http://purl.uniprot.org/taxonomy/>
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX lscr: <http://purl.org/lscr#>
select ?protein1 ?protein2 ?protein2_splice_variant ?uniprot_xref
where {
?cluster a orth:OrthologsCluster.
?cluster orth:hasHomologousMember ?node1.
?cluster orth:hasHomologousMember ?node2. 
?node2 orth:hasHomologousMember* ?protein2. 
?node1 orth:hasHomologousMember* ?protein1.
?protein2 a orth:Protein;
            sio:SIO_010079 ?gene2 .
?protein2_splice_variant sio:SIO_010079 ?gene2 .
?protein2_splice_variant lscr:xrefUniprot ?uniprot_xref .
?protein1 a orth:Protein.
?protein1 sio:SIO_010079 ?gene . 
?gene lscr:xrefEnsemblGene  ensembl:ENSG00000244734 .
filter(?node1 != ?node2) 
} order by ?protein2
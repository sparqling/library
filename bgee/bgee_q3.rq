##
#Q3: what are the anatomic entities a gene defined with an Ensembl identifier (for example, ENSG00000244734) is expressed along with its expression score and developmental stage
###
PREFIX orth: <http://purl.org/net/orth#>
PREFIX genex: <http://purl.org/genex#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX lscr: <http://purl.org/lscr#>
SELECT ?anatEntity ?anatName ?dev_stage ?score ?ensembl_rdf_uri {
    VALUES ?geneID {'ENSG00000244734'} 
	?seq a orth:Gene .
    ?expression a genex:Expression .
	?expression genex:hasSequenceUnit ?seq .
        ?expression genex:hasExpressionLevel ?score . 
        ?expression genex:hasExpressionCondition ?cond .
        ?cond genex:hasAnatomicalEntity ?anatEntity.
        ?cond genex:hasDevelopmentalStage/rdfs:label ?dev_stage .
        ?seq lscr:xrefEnsemblGene ?ensembl_rdf_uri .
        ?seq dct:identifier  ?geneID.
	?anatEntity rdfs:label ?anatName . 
} 